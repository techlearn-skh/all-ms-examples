plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.6'
	id 'io.spring.dependency-management' version '1.1.7'
	id "org.sonarqube" version "6.3.1.5724"
	id 'jacoco' // To enable JaCoCo code coverage
}

group = 'com.skh'
version = '0.0.1-SNAPSHOT'
description = 'retry-consumer-service project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'
}

jacoco {
	toolVersion = "0.8.10"
}

tasks.named('jacocoTestReport', JacocoReport) {
	dependsOn test
	reports {
		xml.required = true
		csv.required = false
		html.required = true
	}
}

tasks.named('jacocoTestCoverageVerification', JacocoCoverageVerification) {
	dependsOn test
	violationRules {
		rule {
			limit {
				counter = 'LINE'
				value = 'COVEREDRATIO'
				minimum = 0.10
			}
		}
	}
}

tasks.named('check') {
	dependsOn tasks.named('jacocoTestCoverageVerification')
}


tasks.named('test') {
	useJUnitPlatform()
	finalizedBy(tasks.named('jacocoTestReport'))
}
